if(__UFW_SetupUFW)
  return()
endif()
set(__UFW_SetupUFW 1)

function(__ufw_toplevel_args)
  cmake_parse_arguments(PA "" "ROOT;ARTIFACTS" "" ${ARGN})
  if (NOT PA_ARTIFACTS)
    message(FATAL_ERROR "ufw_toplevel: Please specify ARTIFACTS destination!")
  endif()
  if (NOT PA_ROOT)
    message(FATAL_ERROR "ufw_toplevel: Please specify UFW ROOT directory!")
  endif()
  set(MICROFRAMEWORK_ROOT "${PA_ROOT}" PARENT_SCOPE)
  set(UFW_ARTIFACTS_DIRECTORY "${PA_ARTIFACTS}" PARENT_SCOPE)
  set(CMAKE_RUNTIME_OUTPUT_DIRECTORY "${PA_ARTIFACTS}" PARENT_SCOPE)
  set(CMAKE_LIBRARY_OUTPUT_DIRECTORY "${PA_ARTIFACTS}" PARENT_SCOPE)
  set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY "${PA_ARTIFACTS}" PARENT_SCOPE)
endfunction()

macro(ufw_toplevel)
  __ufw_toplevel_args(${ARGV})
  include(BuildInTargetDir)
  include(HardwareAbstraction)
  include(CTest)
endmacro()

macro(setup_ufw ufw_root)
  set(MICROFRAMEWORK_ROOT "${CMAKE_CURRENT_SOURCE_DIR}/${ufw_root}")
  add_subdirectory(${ufw_root})
endmacro()
